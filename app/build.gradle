apply plugin: 'com.android.application'


android {
    compileSdkVersion 29
    buildToolsVersion "29.0.2"
    defaultConfig {
        applicationId "com.example.magicplacefinder"
        minSdkVersion 23
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    def props = new Properties()
    file("secret.keys").withInputStream { props.load(it) }
    def PLACES_API_KEY = props.getProperty("placesAPIKey")?: ""
    def MAPS_API_KEY = props.getProperty("mapsAPIKey")?: ""


    buildTypes {

        debug {
            applicationIdSuffix ".debug"
            buildConfigField "String", "ROOT_URL", "\"https://maps.googleapis.com/maps/api/place/\""
            buildConfigField "String", "PLACES_API_KEY", PLACES_API_KEY
            resValue "string", "google_maps_key", MAPS_API_KEY
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        release {
            buildConfigField "String", "ROOT_URL", "\"https://maps.googleapis.com/maps/api/place/\""
            buildConfigField "String", "PLACES_API_KEY", PLACES_API_KEY
            resValue "string", "google_maps_key", MAPS_API_KEY
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.google.android.material:material:1.1.0'
    implementation 'android.arch.lifecycle:extensions:1.1.1'
    implementation 'org.apache.commons:commons-lang3:3.9'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'com.google.android.gms:play-services-base:17.2.1'
    implementation 'com.google.android.gms:play-services-location:17.0.0'
    implementation 'com.google.android.gms:play-services-maps:17.0.0'

    //rest service and gson converter
    implementation 'com.squareup.retrofit2:retrofit:2.6.2'
    implementation 'com.squareup.retrofit2:converter-gson:2.5.0'
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation("com.squareup.okhttp3:logging-interceptor:4.2.1")

    //RxJava
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'com.jakewharton.retrofit:retrofit2-rxjava2-adapter:1.0.0'

    //AndroidX Test
    def androidx_test_version = "1.2.0"
    androidTestImplementation "androidx.test:core:$androidx_test_version"
    androidTestImplementation "androidx.test:runner:$androidx_test_version"

    // JUnit5 for local unit tests
    def junit_jupiter_version = "5.6.1"
    testImplementation "org.junit.jupiter:junit-jupiter-api:$junit_jupiter_version"
    testImplementation "org.junit.jupiter:junit-jupiter-params:$junit_jupiter_version"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junit_jupiter_version"
    testImplementation("com.squareup.okhttp3:mockwebserver:4.4.0")
    testImplementation "android.arch.core:core-testing:1.1.1"

    // Mockito
    def mockito_version = "3.3.0"
    testImplementation "org.mockito:mockito-core:$mockito_version"

    //JUnit4 for instrumentation tests
    testImplementation 'junit:junit:4.12'
}
